<!DOCTYPE html>
<html lang="en">
<head>
    <meta charset="utf-8">
    <title>JSDoc: Source: common.libsonnet</title>

    <script src="scripts/prettify/prettify.js"> </script>
    <script src="scripts/prettify/lang-css.js"> </script>
    <!--[if lt IE 9]>
      <script src="//html5shiv.googlecode.com/svn/trunk/html5.js"></script>
    <![endif]-->
    <link type="text/css" rel="stylesheet" href="styles/prettify-tomorrow.css">
    <link type="text/css" rel="stylesheet" href="styles/jsdoc-default.css">
</head>

<body>

<div id="main">

    <h1 class="page-title">Source: common.libsonnet</h1>

    



    
    <section>
        <article>
            <pre class="prettyprint source linenums"><code>/**
 * @fileoverview
 * Utility functions and configuration that is common throughout the Mimir Jsonnet library.
 */
{
  namespace:
    $.core.v1.namespace.new($._config.namespace),

  util+:: {
    local containerPort = $.core.v1.containerPort,
    local container = $.core.v1.container,

    defaultPorts::
      [
        containerPort.newNamed(name='http-metrics', containerPort=$._config.server_http_port),
        containerPort.newNamed(name='grpc', containerPort=9095),
      ],

    readinessProbe::
      container.mixin.readinessProbe.httpGet.withPath('/ready') +
      container.mixin.readinessProbe.httpGet.withPort($._config.server_http_port) +
      container.mixin.readinessProbe.withInitialDelaySeconds(15) +
      container.mixin.readinessProbe.withTimeoutSeconds(1),

    /**
     * @function serviceForUsingNamedPorts
     * @description Generate a Service resource from either a
     * DaemonSet, Deployment, ReplicaSet, or StatefulSet.
     * The Service selector is derived from the .metadata.labels.
     * All ports for all containers are exposed in the Service.
     * Named arguments match the signature defined in (github.com/grafana/jsonnet-libs/ksonnet-util/util.libsonnet).serviceFor
     * but use camelCase naming.
     * This function asserts behavior that serviceFor assumes, notably that each container port is named.
     * @param {DaemonSet|Deployment|ReplicaSet|StatefulSet} app
     * @param {string[]} [ignoredLabels=[]] -
     * Label names that should be ignored when deriving a selector for the Service.
     * @param {string} [nameFormat='%(container)s-%(port)s' -
     * Format for the Service port names.
     * The format string is interpolated with an object with 'container' and 'port' members
     * for API compatibility with the 'serviceFor' function.
     * @returns {Service}
     */
    serviceForUsingNamedPorts(app, ignoredLabels=[], nameFormat='%(container)s-%(port)s')::
      local container = $.core.v1.container;
      local service = $.core.v1.service;
      local ports =
        std.mapWithIndex(
          function(i, c)
            std.mapWithIndex(
              function(j, p)
                {
                  assert std.objectHas(p, 'name') : |||
                    serviceForUsingNamedPorts: all container ports must have a name.
                    .spec.container[%d].ports[%d] is missing a name.
                  ||| % [i, j],
                  name: nameFormat % { container: c.name, port: p.name },
                  port: p.containerPort,
                  targetPort: p.name,
                  [if std.objectHas(p, 'protocol') then 'protocol']: p.protocol,
                },
              ({ ports: [] } + c).ports,
            ),
          app.spec.template.spec.containers
        );
      local selector =
        local labels = app.spec.template.metadata.labels;
        {
          [x]: labels[x]
          for x in std.objectFields(labels)
          if !std.member(ignoredLabels, x)
        };

      service.new(
        name=app.metadata.name,
        selector=selector,
        ports=ports,
      )
      + service.metadata.withLabels({ name: app.metadata.name }),
  },
}
</code></pre>
        </article>
    </section>




</div>

<nav>
    <h2><a href="index.html">Home</a></h2><h3>Global</h3><ul><li><a href="global.html#serviceForUsingNamedPorts">serviceForUsingNamedPorts</a></li></ul>
</nav>

<br class="clear">

<footer>
    Documentation generated by <a href="https://github.com/jsdoc/jsdoc">JSDoc 3.6.10</a> on Wed Mar 02 2022 17:25:36 GMT+0000 (Greenwich Mean Time)
</footer>

<script> prettyPrint(); </script>
<script src="scripts/linenumber.js"> </script>
</body>
</html>
